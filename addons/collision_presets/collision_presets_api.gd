@tool
extends Node
class_name CollisionPresetsAPI

static var presets_db_static: CollisionPresetsDatabase

## Loads presets resource from disk.
static func _load_static_presets(previous_path: String = ""):
	if presets_db_static == null:
		var path := CollisionPresetsConstants.PRESET_DATABASE_PATH
		if not ResourceLoader.exists(path):
			# Migration/Fallback: check old locations
			var migration_paths := [
				(CollisionPresetsConstants as Script).resource_path.get_base_dir().path_join("presets.tres"), # Original plugin dir
				"res://collision_presets/presets.tres", # Original default project-level dir
			]
			
			if not previous_path.is_empty():
				migration_paths.append(previous_path.path_join("presets.tres"))
			
			var found_old_path := ""
			for p in migration_paths:
				if p != path and ResourceLoader.exists(p):
					found_old_path = p
					break
			
			if not found_old_path.is_empty():
				# Move to new location if it exists in an old one
				var dir := path.get_base_dir()
				if not DirAccess.dir_exists_absolute(dir):
					DirAccess.make_dir_recursive_absolute(dir)
				
				# Log migration to help user track their files
				print("CollisionPresets: Migrating database from ", found_old_path, " to ", path)
				
				var err = DirAccess.rename_absolute(found_old_path, path)
				if err != OK:
					printerr("CollisionPresets: Failed to migrate database: ", err)
					return

				# Also try to move the generated constants script if it exists in the same old directory
				var old_names_path := found_old_path.get_base_dir().path_join("preset_names.gd")
				var new_names_path := CollisionPresetsConstants.PRESET_NAMES_PATH
				if FileAccess.file_exists(old_names_path) and old_names_path != new_names_path:
					print("CollisionPresets: Migrating constants script from ", old_names_path, " to ", new_names_path)
					DirAccess.rename_absolute(old_names_path, new_names_path)
			else:
				# Create directory for new location if it doesn't exist
				var dir := path.get_base_dir()
				if not DirAccess.dir_exists_absolute(dir):
					DirAccess.make_dir_recursive_absolute(dir)
				
		if ResourceLoader.exists(path):
			presets_db_static = ResourceLoader.load(path)

## API: Returns a preset object by name.
static func get_preset(name: String) -> CollisionPreset:
	if presets_db_static == null: _load_static_presets()
	for p in presets_db_static.presets:
		if p.name == name:
			return p
	return null

## API: Returns a preset object by ID.
static func get_preset_by_id(id: String) -> CollisionPreset:
	if id.is_empty():
		return null
	if presets_db_static == null: _load_static_presets()
	for p in presets_db_static.presets:
		if p.id == id:
			return p
	return null

## API: Applies a named preset to the given object and sets its metadata.
static func apply_preset(object: Node, name: String) -> bool:
	var p = get_preset(name)
	if p:
		if "collision_layer" in object:
			object.collision_layer = p.layer
		if "collision_mask" in object:
			object.collision_mask = p.mask
		# Set BOTH name and ID for backward compatibility and robustness.
		object.set_meta(CollisionPresetsConstants.META_KEY, p.name as StringName)
		if not p.id.is_empty():
			object.set_meta(CollisionPresetsConstants.META_ID_KEY, p.id as StringName)
		return true
	return false

## API: Returns the collision layer of a preset.
static func get_preset_layer(name: String) -> int:
	var p = get_preset(name)
	return p.layer if p else 0

## API: Returns the collision mask of a preset.
static func get_preset_mask(name: String) -> int:
	var p = get_preset(name)
	return p.mask if p else 0

## API: Returns all available preset names.
static func get_preset_names() -> Array[String]:
	if presets_db_static == null: _load_static_presets()
	var names: Array[String] = []
	for p in presets_db_static.presets:
		names.append(p.name)
	return names
	
## API: Retursn the collision layer of multiple presets.
static func get_combined_presets_layer(names: Array[String]) -> int:
	var layer := 0
	for name in names:
		layer |= get_preset_layer(name)
	return layer
	
## API: Returns the collision mask of multiple presets.
static func get_combined_presets_mask(names: Array[String]) -> int:
	var mask := 0
	for name in names:
		mask |= get_preset_mask(name)
	return mask

## API: Returns the preset name of a node.
static func get_node_preset(node: Node) -> String:
	if presets_db_static == null: _load_static_presets()
	
	# Try to find by ID first for robustness (handles renames)
	if node.has_meta(CollisionPresetsConstants.META_ID_KEY):
		var id = str(node.get_meta(CollisionPresetsConstants.META_ID_KEY))
		var p = get_preset_by_id(id)
		if p:
			return p.name
	
	# Fallback to name-based meta
	if node.has_meta(CollisionPresetsConstants.META_KEY):
		return str(node.get_meta(CollisionPresetsConstants.META_KEY))
	
	# Fallback to default preset name if no meta
	var def = get_preset_by_id(presets_db_static.default_preset_id)
	if def:
		return def.name
	
	return ""

## API: Static way to set a preset on a node. Safe to use at tool time.
static func set_node_preset(node: Node, preset_name: String) -> bool:
	if presets_db_static == null: _load_static_presets()
	
	if preset_name == "":
		# Default: remove metadata and apply default values
		if node.has_meta(CollisionPresetsConstants.META_KEY):
			node.remove_meta(CollisionPresetsConstants.META_KEY)
		if node.has_meta(CollisionPresetsConstants.META_ID_KEY):
			node.remove_meta(CollisionPresetsConstants.META_ID_KEY)
		var p = get_preset_by_id(presets_db_static.default_preset_id)
		if p:
			if "collision_layer" in node:
				node.collision_layer = p.layer
			if "collision_mask" in node:
				node.collision_mask = p.mask
			return true
		return true

	if preset_name == "__custom__":
		# Custom: set metadata to __custom__ and do nothing else
		node.set_meta(CollisionPresetsConstants.META_KEY, "__custom__" as StringName)
		if node.has_meta(CollisionPresetsConstants.META_ID_KEY):
			node.remove_meta(CollisionPresetsConstants.META_ID_KEY)
		return true

	for p in presets_db_static.presets:
		if p.name == preset_name:
			return apply_preset(node, preset_name)
	
	return false

## API: Generates or refreshes a script with constants for preset names.
static func generate_preset_constants_script(db: CollisionPresetsDatabase = null):
	if db == null:
		_load_static_presets()
		db = presets_db_static
	
	if db == null:
		return

	# Build list of valid, unique identifiers mapped to original names
	var used := {}
	var idents: Array[String] = []
	var lines: Array[String] = []
	lines.append("# This file is auto-generated by the Collision Presets editor plugin.\n# Do not edit manually.\n")
	lines.append("class_name CollisionPresets\n")
	# Constants
	for p in db.presets:
		var ident := get_identifier(p.name, used)
		idents.append(ident)
		lines.append("const %s := \"%s\"\n" % [ident, p.name])
	# Static all()
	lines.append("\nstatic func all() -> PackedStringArray:\n")
	lines.append("\treturn PackedStringArray([\n")
	for i in range(idents.size()):
		lines.append("\t\t%s%s\n" % [idents[i], "," if i < idents.size()-1 else ""]) 
	lines.append("\t])\n")
	
	var content := "".join(lines)
	# Only write when changed to avoid VCS noise
	var existing := ""
	if FileAccess.file_exists(CollisionPresetsConstants.PRESET_NAMES_PATH):
		var f := FileAccess.open(CollisionPresetsConstants.PRESET_NAMES_PATH, FileAccess.READ)
		if f:
			existing = f.get_as_text()
			f.close()
	
	if existing != content:
		var f2 := FileAccess.open(CollisionPresetsConstants.PRESET_NAMES_PATH, FileAccess.WRITE)
		if f2:
			f2.store_string(content)
			f2.flush()
			f2.close()

## Turn arbitrary preset names into valid GDScript constant identifiers.
static func get_identifier(name: String, used: Dictionary = {}) -> String:
	var s := name.strip_edges()
	if s.is_empty():
		s = "Preset"
	# Replace invalid characters with '_'
	var out := ""
	for ch in s:
		if (ch >= "A" and ch <= "Z") or (ch >= "a" and ch <= "z") or (ch >= "0" and ch <= "9") or ch == "_":
			out += ch
		else:
			out += "_"
	# If first char is digit, prefix underscore
	if out.length() > 0 and out[0] >= "0" and out[0] <= "9":
		out = "_" + out
	# Collapse multiple underscores
	while out.find("__") != -1:
		out = out.replace("__", "_")
	# Ensure not empty
	if out.is_empty():
		out = "Preset"
	# Enforce uniqueness if a map is provided
	if used != null:
		var base := out
		var n := 1
		while used.has(out):
			out = "%s_%d" % [base, n]
			n += 1
		used[out] = true
	return out
